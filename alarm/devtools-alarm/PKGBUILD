# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patches to fix issues with how we use the tools

pkgname=devtools-alarm
_pkgname=devtools
pkgver=20190329
pkgrel=1
pkgdesc='Tools for Arch Linux ARM package maintainers'
arch=('any')
license=('GPL')
url='https://git.archlinux.org/devtools.git/'
conflicts=('devtools')
provides=('devtools')
depends=('openssh' 'subversion' 'rsync' 'arch-install-scripts'
         'git' 'bzr' 'mercurial' 'diffutils')
makedepends=('asciidoc')
optdepends=('btrfs-progs: btrfs support')
source=(https://sources.archlinux.org/other/${_pkgname}/${_pkgname}-${pkgver}.tar.gz{,.sig}
        '0001-makechrootpkg-cache-dir.patch'
        '0002-arch-nspawn-keep-mirrorlist.patch'
        '0003-makechrootpkg-distcc.patch'
        '0004-arch-nspawn-arm-fix.patch'
        '0005-makechrootpkg-no-default-logging.patch'
        '0006-archbuild-no-setarch.patch'
        '0007-makechrootpkg-don-t-delete-MAKEFLAGS-and-PACKAGER.patch')
validpgpkeys=('487EACC08557AD082088DABA1EB2638FF56C0C53'
              '4AA4767BBC9C4B1D18AE28B77F2D434B9741E8AC'
              '86CFFCA918CF3AF47147588051E8B148A9999C34'
              '8FC15A064950A99DD1BD14DD39E4B877E62EB915'
              '8218F88849AAC522E94CF470A5E9288C4FA415FA'
              'B81B051F2D7FC867AAFF35A58DBD63B82072D77A'
              'F3691687D867B81B51CE07D9BBE43771487328A9'
              '6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'
              'E240B57E2C4630BA768E2F26FC1B547C8D8172C8')
sha256sums=('22f657a2489139f6213752836881415460853fe6eb9d1a84022931e66f98c45f'
            'SKIP'
            '24b051855944acc377ab9c74481c2c434241e8ea51942f843a1ef875d459b0ba'
            'ba23180de0e0b66a5286d0ab272ee5d0ba29a7bb553beefa0d303a45550aff37'
            '3d6ea42aab993b839d59c951d7096965428919d56eede6bfc71e97cbac3c4e71'
            'c0b62e423ff4ec9801a7251c7a04ac780c313e949cb66d9f9421a51a87e56e56'
            '86ed7e593a3caffd781a1e618d0e191173f064851694e263cdf50a6e6b16f61a'
            '57c42af2b8af7d9140bac0a772d47168d74fff52de40f6580913a65baf3e885c'
            '148caa91d60505327ec2c8ca118e91a7f3b497d2b3251e2f3512094e8a48c73d')

prepare() {
  cd "${_pkgname}-${pkgver}"
  patch -p1 -i ../0001-makechrootpkg-cache-dir.patch
  patch -p1 -i ../0002-arch-nspawn-keep-mirrorlist.patch
  patch -p1 -i ../0003-makechrootpkg-distcc.patch
  patch -p1 -i ../0004-arch-nspawn-arm-fix.patch
  patch -p1 -i ../0005-makechrootpkg-no-default-logging.patch
  patch -p1 -i ../0006-archbuild-no-setarch.patch
  patch -p1 -i ../0007-makechrootpkg-don-t-delete-MAKEFLAGS-and-PACKAGER.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  make PREFIX=/usr
}

package() {
  cd ${_pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
