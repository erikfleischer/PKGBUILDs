# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to remove MMX and NEON headers

pkgname=coin-or-clp
pkgver=1.17.3
pkgrel=1
pkgdesc="COIN-OR linear programming solver"
arch=(x86_64)
url="https://projects.coin-or.org/Clp"
license=(EPL)
groups=(coin-or)
depends=(coin-or-osi suitesparse)
source=("https://www.coin-or.org/download/source/Clp/Clp-${pkgver}.tgz"
        0001-Remove-MMX-and-NEON-headers.patch)
sha256sums=('a13bf54291ad503cf76f5f93f2643d2add4faa5d0e60ff2db902ef715c094573'
            '0d778e38571919a804a0c98e8182b768f7d069f39cf01cc22f551d0ff18317c1')

prepare() {
  cd Clp-$pkgver/Clp
  patch -p2 -i $srcdir/0001-Remove-MMX-and-NEON-headers.patch
}

build() {
  cd Clp-$pkgver/Clp
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-osi-lib="$(pkg-config --libs osi)" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" \
              --with-cholmod-lib='-lcholmod' --with-cholmod-incdir=/usr/include
  make
}

check() {
  cd Clp-$pkgver/Clp
# make test
}

package() {
  cd Clp-$pkgver/Clp
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir" install
}
