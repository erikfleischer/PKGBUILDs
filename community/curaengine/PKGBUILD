# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Grey Christoforo <first name [at] last name [dot] net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - -DBUILD_TESTS=OFF

pkgname=curaengine
pkgver=4.2.0
pkgrel=1
pkgdesc="Engine for processing 3D models into 3D printing instruction for Ultimaker and other GCode based 3D printers."
url="https://github.com/Ultimaker/CuraEngine"
arch=('x86_64')
license=('AGPL')
depends=('arcus' 'rapidjson')
# TODO: https://github.com/Ultimaker/CuraEngine/blob/master/CMakeLists.txt#L26
# Work with system openmp
# Package polyclipping in the repos
checkdepends=('cppunit')
makedepends=('cmake' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Ultimaker/${pkgname}/archive/${pkgver}.tar.gz"
        https://src.fedoraproject.org/rpms/CuraEngine/raw/master/f/CuraEngine-gcc9.patch)
sha512sums=('6c3edac5a21068b50a31d3ab37dac58b986e18be2d24711d77cf210da62b521d62dc92a1b4e8865a26e5c394119349f03b3816d19d67ebee670424cc4a7b2500'
            'fe3130992113d850322ac1ba34cda3e1ab146a155097c9787a2044e78751de32590b00862c7a4c1ce0ac95281a7e669509741e9f9a170f23ad6eaec13e01bc2b')

prepare() {
  cd CuraEngine-${pkgver}
  patch -Np1 -i ${srcdir}/CuraEngine-gcc9.patch
}

build() {
  cd CuraEngine-${pkgver}
  mkdir -p build
  cd build
    
   cmake .. \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DBUILD_TESTS=OFF \
     -DCMAKE_BUILD_TYPE=Release

  make
}

check() {
  cd CuraEngine-${pkgver}/build
  make test
}

package() {
  cd CuraEngine-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
