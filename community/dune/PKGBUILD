# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream-ish patch for ARMv7
#    https://github.com/ocaml/dune/issues/2527

pkgname=dune
pkgver=1.11.1
pkgrel=2
pkgdesc="A composable build system for OCaml (formerly jbuilder)"
arch=(x86_64)
url="https://github.com/ocaml/dune"
license=(Apache)
depends=(glibc ocaml ocaml-findlib)
provides=(jbuilder)
conflicts=(jbuilder)
replaces=(jbuilder)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '15c04b09a8c06871635d5fd98c3a37089bbde6d9.patch')
sha256sums=('f2aa962451c98e47a90ca47273b2080e5f9aaeb6e3b6271dc78a44b831cc0d46'
            '156de7635cb3a135d3e37eeaeef665f613b3e708ca7d5cb5f078b1f1efbeac6d')

prepare() {
    cd ${pkgname}-${pkgver}
    if [[ $CARCH == "armv7h" ]]; then
        patch -p1 -i ../15c04b09a8c06871635d5fd98c3a37089bbde6d9.patch
    fi
}

build() {
    cd ${pkgname}-${pkgver}
    make release
}

# Tests requires a bunch of (currently) unpackaged dependencies
#check() {
#    cd ${pkgname}-${pkgver/b/-beta}
#    make test
#}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" INSTALL_ARGS="--prefix=/usr --libdir='$(ocamlfind printconf destdir)'" install

    # Fix doc and man install
    rm -r "${pkgdir}"/usr/doc
    install -dm755 "${pkgdir}"/usr/share
    mv "${pkgdir}"/usr/{man,share/}
}
