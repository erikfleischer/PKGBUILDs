# Maintainer : Sven-Hendrik Haase <svenstaro@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=openvdb
pkgver=6.1.0
pkgrel=1
pkgdesc='A large suite of tools for the efficient storage and manipulation of sparse volumetric data discretized on three-dimensional grids'
url='https://github.com/dreamworksanimation/openvdb'
arch=('x86_64')
license=('MPL')
depends=('openexr' 'boost-libs' 'intel-tbb' 'zlib' 'jemalloc' 'blosc')
makedepends=('doxygen' 'boost' 'cmake' 'mesa' 'cppunit' 'glfw-x11' 'glu' 'python' 'python-numpy')
optdepends=('glfw: for tools'
            'glu: for tools'
            'python-numpy: python module')
source=("https://github.com/dreamworksanimation/openvdb/archive/v${pkgver}.tar.gz"
        https://patch-diff.githubusercontent.com/raw/AcademySoftwareFoundation/openvdb/pull/428.patch)
sha512sums=('99ebbb50104ef87792ab73989e8714c4f283fb02d04c3033126b5f0d927ff7bbdebe35c8214ded841692941d8ed8ae551fd6d1bf90ad7dc07bedc3b38b9c4b38'
            '2eba70b5dde0d9f319841963a93f74915ecc65a13629fec282a04ceb260f30dd475c43b445e7bb121dbc0381c638ddbae102775279aef46b80b1d3093497b916')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i "s|DESTINATION docs|DESTINATION share/doc/$pkgname|" openvdb/CMakeLists.txt
  sed -i "s/MINIMUM_PYTHON_VERSION 2.7/MINIMUM_PYTHON_VERSION 3.7/g" CMakeLists.txt

  patch -Np1 -i "$srcdir"/428.patch

  mkdir build
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"/build

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && LDFLAGS="-latomic $LDFLAGS"
  export CXXFLAGS="${CXXFLAGS} -DPY_OPENVDB_USE_NUMPY"
  cmake .. \
    -DBLOSC_LOCATION=/usr/ \
    -DTBB_LOCATION=/usr/ \
    -DUSE_GLFW3=ON \
    -DGLFW3_LOCATION=/usr/ \
    -DILMBASE_NAMESPACE_VERSIONING=OFF \
    -DOPENEXR_NAMESPACE_VERSIONING=OFF \
    -DILMBASE_LOCATION=/usr/ \
    -DOPENEXR_LOCATION=/usr/ \
    -DCPPUNIT_LOCATION=/usr/include/cppunit \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOPENVDB_BUILD_PYTHON_MODULE=ON \
    -DOPENVDB_BUILD_DOCS=ON \
    -DOPENVDB_BUILD_UNITTESTS=OFF \
    -DPYTHON_INCL_DIR=/usr/include/python3.7m \
    -DPYTHON_INCLUDE_DIR=/usr/include/python3.7m \
    -DPYTHON_LIB_DIR=/usr/lib \
    -DPython_LIBRARY=/usr/lib/libpython3.7m.so \

  sed -i "s/isystem/I/g" $(find . -name flags.make)
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}" install
}

# vim:set sw=2 sts=2 et:
