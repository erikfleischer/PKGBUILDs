# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepends on pandoc

pkgname=owl-lisp
pkgver=0.1.19
pkgrel=2
pkgdesc='Simple purely functional lisp'
url='https://gitlab.com/owl-lisp/owl.git'
arch=('x86_64')
license=('MIT')
depends=('glibc')
source=(https://gitlab.com/owl-lisp/owl/-/archive/v${pkgver}/owl-v${pkgver}.tar.gz)
sha256sums=('d02b49c3bba48aec8e83040d39a5d4fe7b91c284aaff7bad0f2efd0b97cf2fee')
sha512sums=('0cb579f94d4fe1bf4c0923d2d58bb03249d8d9444c3452f261a7485ac0d15c08dbfba2388514b8b8fa93409e49ed346b192760be6bd9dab19f1def4e322be1c7')

prepare() {
  cd owl-v${pkgver}

  # support distro CFLAGS and CPPFLAGS
  sed -e '/export CFLAGS/d' -e '/CFLAGS =/d' \
      -e 's|$(CFLAGS)|$(CFLAGS) $(CPPFLAGS)|g' -i Makefile

  # make build reproducible, we compress ourselves
  sed -e 's|gzip -9|@echo|g' -e 's|\.gz||g' -i Makefile

  # fix missing Makefile entry
  sed 's|manual.md:|manual.md: bin/ol|' -i Makefile
}

build() {
  cd owl-v${pkgver}
  make owl documentation
}

check() {
  cd owl-v${pkgver}
  make -j1 test fasltest
}

package() {
  cd owl-v${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 fasl/* -t "${pkgdir}/var/lib/${pkgname}/fasl"
  install -Dm 644 LICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md manual.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
