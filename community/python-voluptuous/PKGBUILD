# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepends on python-pypandoc, python2-pypandoc

pkgbase=python-voluptuous
pkgname=(python-voluptuous python2-voluptuous)
pkgver=0.11.7
pkgrel=1
pkgdesc="A Python data validation library"
url="https://github.com/alecthomas/voluptuous"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("https://pypi.io/packages/source/v/voluptuous/voluptuous-$pkgver.tar.gz")
sha512sums=('bf0ad4684a4da59b3eccb54f6edfa643eb894076fc50d1bfd4c953de15cfd711a70e8aee685497197eaef24897ae55fb5fb2aec5e2fc9dcf344a6f7c6f569b88')

prepare() {
  # https://github.com/alecthomas/voluptuous/issues/333
  rm -r voluptuous-$pkgver/*.egg-info

  # https://github.com/alecthomas/voluptuous/pull/332
  sed -e "s/'w'/'wb'/" -e "s/.encode('utf-8')//" -e "s/f.write(long_description)/f.write(long_description.encode('utf-8'))/" -i voluptuous-$pkgver/setup.py

  # Python 3.7
  sed -i '/raise StopIteration/d' voluptuous-$pkgver/voluptuous/schema_builder.py

  cp -a voluptuous-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/voluptuous-$pkgver
  python setup.py build

  cd "$srcdir"/voluptuous-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/voluptuous-$pkgver
  python setup.py nosetests

  cd "$srcdir"/voluptuous-$pkgver-py2
  python2 setup.py nosetests
}

package_python-voluptuous() {
  depends=('python')

  cd voluptuous-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_python2-voluptuous() {
  depends=('python2')

  cd voluptuous-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
