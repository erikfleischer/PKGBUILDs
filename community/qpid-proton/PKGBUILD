# Maintainer: Nicola Squartini <tensor5@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable LTO

pkgname=(qpid-proton python2-qpid-proton)
pkgver=0.29.0
pkgrel=1
pkgdesc='High-performance, lightweight messaging library'
arch=('x86_64')
url='https://qpid.apache.org/proton'
license=('Apache')
makedepends=('cmake' 'doxygen' 'python' 'python2' 'swig')
source=("https://www.apache.org/dist/qpid/proton/${pkgver}/qpid-proton-${pkgver}.tar.gz")
sha512sums=('d6b7bc9811230c48b0c10c3cc076a6de6b29c5e22d69556f83e21110eeb9a105d11328b2993694a2e39f0681007b4b3144270ea18efd456a802a08cb564ae4a4')

build() {
    cd ${pkgname}-${pkgver}

    mkdir build
    cd build
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_SUFFIX='' \
        -DSYSINSTALL_BINDINGS=ON \
        -DENABLE_LINKTIME_OPTIMIZATION=OFF \
        ..
    make

    cd python/dist
    python2 setup.py build
}

package_qpid-proton() {
    depends=('jsoncpp' 'python' 'ruby')

    cd ${pkgname}-${pkgver}/build
    make DESTDIR="${pkgdir}" install
}

package_python2-qpid-proton() {
    depends=('python2')

    cd qpid-proton-${pkgver}/build/python/dist
    python2 setup.py install --root="${pkgdir}" --skip-build --optimize=1
}
