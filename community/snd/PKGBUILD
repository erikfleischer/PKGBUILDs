# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: see .contrib

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with pulseaudio as sound server, not jack (x86 asm)

pkgname=snd
pkgver=19.7
pkgrel=1
pkgdesc="An advanced sound editor"
arch=('x86_64')
url="https://ccrma.stanford.edu/software/snd/"
license=('custom:free')
groups=('pro-audio')
depends=('alsa-lib' 'cairo' 'fftw' 'glibc' 'glib2' 'gmp' 'gsl' 'gtk3' 'pulseaudio'
'libmpc' 'libsamplerate' 'pango' 'ruby')
makedepends=('flac' 'gendesk' 'glu' 'ladspa' 'libpulse' 'libxpm' 'mpg123'
'openmotif' 'speex' 'timidity++' 'vorbis-tools' 'wavpack')
optdepends=('wavpack: For wav support'
            'flac: For flac support'
            'speex: For speex support'
            'mpg123: For MPEG support'
            'timidity++: For MIDI to WAVE support'
            'vorbis-tools: For OGG support')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e3d856baa0b136352d3d4e3b9228faf87ee9d7809c5a6523f6e6ff169bc9fb959c9f70a5daad612441dfb888d35e0dfeca8910940241c1fbf017f52b47188e80')

prepare() {
  cd "${pkgname}-${pkgver}"
  # create XDG desktop file, as the default is broken
  gendesk -f -n \
          --pkgname "${pkgname}" \
          --name "${pkgname}" \
          --exec "${pkgname}" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gmp \
              --with-gtk \
              --with-ladspa \
              --with-pulseaudio \
              --with-ruby
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {{HISTORY,README}.Snd,NEWS} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 *.html -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  install -vDm 644 pix/*.png -t "${pkgdir}/usr/share/doc/${pkgname}/html/pix"
}

# vim:set ts=2 sw=2 et:
