# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to skip qt5 cocoa check, broken on ARM

pkgname=suil
pkgver=0.10.4
pkgrel=1
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs"
arch=('x86_64')
url="https://drobilla.net/software/suil/"
license=('custom:ISC')
makedepends=('gtk2' 'gtk3' 'lv2' 'python' 'qt5-base')
optdepends=('qt5-base: Qt5 UI wrapping support'
            'gtk2: GTK+ 2.x UI wrapping support'
            'gtk3: GTK+ 3.x UI wrapping support')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        '0001-remove-cocoa-check.patch')
sha512sums=('1f5627541b86cbd8417c0b35b04b73340a3b2dbdd7b04314fdfa82baa8a7b0813c9c9914f805fcf233bcc8011b37efe99a06b6c47a3dda9606d6493edcba434f'
            'SKIP'
            '49fc53bfccb6bdd297ffb9355c0e72eb2e8fe1f2785614ca072aa1fcbc05a1aba6f62830551911826441cd0fcfbe20c8135a0f3ada9d589b32b43394962ec8c3')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove local call to ldconfig
  sed -i "/ldconfig/d" wscript
  patch -p1 -i ../0001-remove-cocoa-check.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  python waf configure --prefix=/usr
  python waf build
}

package() {
  cd "${pkgname}-${pkgver}"
  python waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "$pkgdir/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,NEWS,PACKAGING,README.md}
}

# vim:set ts=2 sw=2 et:
