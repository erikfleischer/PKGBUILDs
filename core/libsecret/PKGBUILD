# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on valgrind
#  - patch to fix building without valgrind

pkgname=libsecret
pkgver=0.18.8
pkgrel=2
pkgdesc="Library for storing and retrieving passwords and other secrets"
url="https://wiki.gnome.org/Projects/Libsecret"
arch=(x86_64)
license=(LGPL)
depends=(glib2 libgcrypt)
checkdepends=(python-dbus dbus-glib python-gobject gjs)
makedepends=(docbook-xsl gobject-introspection vala git gtk-doc meson)
optdepends=('gnome-keyring: key storage service, or use any other service implementing org.freedesktop.secrets')
_commit=b5442654d483e959ac9ecd3a3fb9eebc8d9d8399  # tags/0.18.8^0
source=("git+https://gitlab.gnome.org/GNOME/libsecret.git#commit=$_commit"
        '0001-Fix-for-building-without-valgrind.patch')
sha256sums=('SKIP'
            '0dfdbe7bbccc388f7d76ac951c344aeec01f30e07fb64c3274d1b907c6b06ee8')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -p1 -i ../0001-Fix-for-building-without-valgrind.patch
}

build() {
  CFLAGS+=" -DHAVE_MLOCK"  # https://gitlab.gnome.org/GNOME/libsecret/issues/23
  arch-meson $pkgname build
  ninja -C build
}

check() {
  dbus-run-session meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
