# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich
#  - remove valgrind makedepend
#  - move gtk3 from checkdepend to makedepend

pkgname=gstreamer
pkgver=1.16.0
pkgrel=2
pkgdesc="GStreamer open-source multimedia framework core library"
url="https://gstreamer.freedesktop.org/"
arch=(x86_64)
license=(LGPL)
depends=(libxml2 glib2 libunwind libcap libelf)
makedepends=(gtk-doc gobject-introspection git bash-completion gtk3 meson)
checkdepends=(gmp gsl)
_commit=89c221a6972513f629c969f5485f37e77ccfa6e1  # tags/1.16.0^0
install=gstreamer.install
source=("git+https://gitlab.freedesktop.org/gstreamer/gstreamer.git#commit=$_commit"
        "gst-common::git+https://gitlab.freedesktop.org/gstreamer/common.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git submodule update
}

build() {
  arch-meson $pkgname build \
    -D ptp-helper-permissions=capabilities \
    -D dbghelp=disabled \
    -D gobject-cast-checks=disabled \
    -D glib-asserts=disabled \
    -D glib-checks=disabled \
    -D package-name="GStreamer (Arch Linux)" \
    -D package-origin="https://www.archlinux.org/"
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
